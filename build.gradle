plugins {
  id 'com.github.johnrengelman.shadow' version '4.0.4'
  id 'java'
}

apply plugin: 'idea'
/* apply plugin: 'java' */

group 'ch.securify'

def computeVersion() {
  if (System.env.SECURIFY_VERSION) {
    return System.env.SECURIFY_VERSION;
  }

  def default_version = "unknown_version"

  try {
    def dirty = "git diff-index --quiet HEAD".execute()
    dirty.waitFor()

    def suffix = dirty.exitValue() ?  "-dirty" : ""

    def proc = "git describe --long".execute()
    proc.waitFor()

    return proc.exitValue() ? default_version : proc.text.trim() + suffix
  } catch (IOException e) {
    return default_version
  }
}


jar {
    manifest {
        attributes 'Main-Class': 'ch.securify.Main'
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': computeVersion()
                    )
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task compileSouffle(type: Exec) {
    doFirst {mkdir project.buildDir}
    executable "./build_souffle.sh"
}

compileJava.dependsOn compileSouffle

compileJava {
    options.debug = true
    options.debugOptions.debugLevel = "source,vars,lines"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.beust:jcommander:1.48"
    compile group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-csv
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.4'

    // Log4j2 dependencies
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}